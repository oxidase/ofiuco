load("@bazel_skylib//lib:paths.bzl", "paths")
load("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")
load("@bazel_tools//tools/build_defs/repo:utils.bzl", "maybe")
load("//lib/private:paths.bzl", "pathsep")

_PACKAGES_ENDPOINT = "https://files.pythonhosted.org/packages/"

# curl -s --header 'Accept: application/vnd.pypi.simple.v1+json' https://pypi.org/simple/pip/ | jq -r '.files[] | "\"\(.url)\", \"\(.hashes.sha256)\""'
_INTERNAL_DEPS = [
    (
        "pip",
        _PACKAGES_ENDPOINT + "44/3c/d717024885424591d5376220b5e836c2d5293ce2011523c9de23ff7bf068/pip-25.3-py3-none-any.whl",
        "9655943313a94722b7774661c21049070f6bbb0a1516bf02f7c8d5d9201514cd",
        ["@ofiuco//python:patches/scripts_executable.patch"],
    ),
]

_POETRY_VERSION = "2.2.1"

def _poetry_deps_repo_impl(rctx):
    interpreter = rctx.path(rctx.attr.python_host)

    args = [interpreter, "-m", "pip", "install", "poetry=={}".format(_POETRY_VERSION), "--target", rctx.attr.output]
    result = rctx.execute(
        args,
        environment = {
            "PYTHONPATH": pathsep(rctx).join([str(rctx.path(dep).dirname) for dep in rctx.attr.deps]),
        },
    )
    if result.return_code != 0:
        fail("{} failed with code {}\n{}\n{}".format(" ".join(args), result.return_code), result.stderr, result.stdout)

    rctx.file("BUILD", """load("@rules_python//python:defs.bzl", "py_library")

exports_files(["defs.bzl"])

py_library(
    name = "pkg",
    srcs = glob(include=["{output}/**/*.py"]),
    data = glob(include=["{output}/**/*"], exclude=[
        "{output}/*/bin/**/*",
        "**/*.py",
        "**/*.pyc",
        "**/* *",
        "**/*.dist-info/RECORD",
    ]),
    visibility = ["//visibility:public"],
    imports = ["{output}"],
)""".format(output = rctx.attr.output))

poetry_deps_repo = repository_rule(
    implementation = _poetry_deps_repo_impl,
    attrs = {
        "deps": attr.label_list(),
        "output": attr.string(),
        "python_host": attr.label(),
    },
)

def _internal_definitions_repo_impl(rctx):
    banner = "# Generated by _internal_definitions_repo_impl in ofiuco/python/repositories.bzl"
    python_host_path = rctx.path(rctx.attr.python_host).realpath.dirname
    if not [path for path in python_host_path.readdir() if path.basename in ["BUILD", "BUILD.bazel"]]:
        python_host_path = python_host_path.dirname
    if not [path for path in python_host_path.readdir() if path.basename in ["BUILD", "BUILD.bazel"]]:
        fail("can not find build file for {} at {}".format(rctx.attr.python_host, rctx.os.name))
    if rctx.os.name.startswith('windows') and rctx.attr.python_host.workspace_name in python_host_path.basename:
        fail("on windows use 'startup --windows_enable_symlinks' to link real Python toolchain path")

    for target in python_host_path.readdir():
        rctx.symlink(target, target.basename)

    rctx.file("defs.bzl", """{banner}

python_host = "{python_host}"
python_host_runtime = "@@{resolved_python_host}//:py3_runtime"
python_version = "{python_version}"
python_toolchain_prefix = "{python_toolchain_prefix}"
""".format(
        banner = banner,
        python_host = rctx.attr.python_host,
        resolved_python_host = rctx.name,
        python_version = rctx.attr.python_version,
        python_toolchain_prefix = rctx.attr.python_toolchain_prefix,
    ))

internal_definitions_repo = repository_rule(
    implementation = _internal_definitions_repo_impl,
    attrs = {
        "python_host": attr.label(),
        "python_version": attr.string(),
        "python_toolchain_prefix": attr.string(),
    },
)

def install_dependencies(python_host, python_version, auth_patterns = {}, netrc = ""):
    prefix = "ofiuco_"

    internal_definitions_repo(
        name = prefix + "defs",
        # Ref: https://github.com/bazelbuild/rules_python/blob/084b877c/python/repositories.bzl#L653-L658
        python_host = python_host,
        python_version = python_version,
        python_toolchain_prefix = python_host.strip("@").split("_3_")[0],
    )

    for (name, url, sha256, patches) in _INTERNAL_DEPS:
        maybe(
            http_archive,
            prefix + name,
            url = url,
            sha256 = sha256,
            type = "zip",
            patches = patches,
            auth_patterns = auth_patterns,
            netrc = netrc,
            build_file_content = """load("@rules_python//python:defs.bzl", "py_library")

py_library(
    name = "pkg",
    srcs = glob(include=["**/*.py"]),
    data = glob(include=["**/*"], exclude=[
        "bin/**/*",
        "**/__pycache__/**",
        "**/*.py",
        "**/*.pyc",
        "**/* *",
        "**/*.dist-info/RECORD",
    ]),
    visibility = ["//visibility:public"],
    imports = ["."],
)
""",
        )

    poetry_deps_repo(
        name = prefix + "poetry_deps",
        output = "site-packages",
        python_host = python_host,
        deps = [
            "@{}pip//:BUILD.bazel".format(prefix),
        ],
    )
